# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")

template("plugin_ndk_raw_file_static") {
  forward_variables_from(invoker, "*")

  ohos_source_set(target_name) {
    defines += invoker.defines
    cflags_cc += invoker.cflags_cc

    include_dirs = [
      "//base/global/resource_management/frameworks/resmgr/include",
      "//third_party/icu/icu4c/source/i18n",
    ]

    cflags = [ "-Wno-error=inconsistent-missing-override" ]

    sources = [ "core/raw_file_manager.cpp" ]

    public_deps = [
      "//foundation/arkui/napi/:ace_napi",
      "//plugins/interfaces/native:plugin_log",
    ]

    if (target_os == "android") {
      public_deps +=
          [ "$ace_root/adapter/android/dependence/resmgr:global_resmgr" ]
    } else if (target_os == "ios") {
      public_deps +=
          [ "$ace_root/adapter/ios/dependence/resmgr:global_resmgr_source_set" ]
    }

    defines += [ "__ARKUI_CROSS__" ]

    subsystem_name = "plugins"
    part_name = "ndk_raw_file"
  }
}

foreach(item, ace_platforms) {
  plugin_ndk_raw_file_static("ndk_raw_file_static_" + item.name) {
    defines = []
    cflags_cc = []
    config = {
    }

    if (defined(item.config)) {
      config = item.config
    }

    if (defined(config.defines)) {
      defines = config.defines
    }

    if (defined(config.cflags_cc)) {
      cflags_cc = config.cflags_cc
    }
  }
}
