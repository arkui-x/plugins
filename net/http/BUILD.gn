# Copyright (c) 2023-2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//plugins/net/http/http.gni")

template("plugin_net_http_static") {
  forward_variables_from(invoker, "*")

  ohos_source_set(target_name) {
    defines += invoker.defines
    cflags_cc += invoker.cflags_cc

    include_dirs = http_include
    include_dirs += utils_include

    sources = [
      "//plugins/net/http/cache/base64_utils.cpp",
      "//plugins/net/http/cache/cache_proxy.cpp",
      "//plugins/net/http/http_module.cpp",
    ]

    deps = [
      "//plugins/interfaces/native:ace_plugin_util_${platform}",
      "//plugins/libs/napi:napi_${target_os}",
      "//plugins/libs/securec:sec_${target_os}",
    ]

    if (platform == "android") {
      include_dirs += [
        "$NETSTACK_NAPI_ROOT/http/cache/lru_cache/include",
        "//third_party/cJSON",
      ]
      deps += [
        "android:net_http_android",
        "android/java:http_android_jni",
      ]
    } else if (platform == "ios") {
      include_dirs += [ "//plugins/net/http/ios/lru_cache/include/" ]
      deps += [ "ios:net_http_ios" ]
    }

    subsystem_name = "plugins"
    part_name = "net_http"
  }
}

foreach(item, ace_platforms) {
  plugin_net_http_static("net_http_static_" + item.name) {
    platform = item.name

    defines = []
    cflags_cc = []
    config = {
    }

    if (defined(item.config)) {
      config = item.config
    }

    if (defined(config.defines)) {
      defines = config.defines
    }

    if (defined(config.cflags_cc)) {
      cflags_cc = config.cflags_cc
    }
  }
}
