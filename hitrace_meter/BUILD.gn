# Copyright (c) 2022-2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")

template("plugin_hitrace_meter_static") {
  forward_variables_from(invoker, "*")

  ohos_source_set(target_name) {
    defines += invoker.defines
    cflags_cc += invoker.cflags_cc

    include_dirs = [
      "//base/hiviewdfx/hitrace/interfaces/native/innerkits/include/hitrace_meter",
      "//base/hiviewdfx/hitrace/interfaces/js/kits/napi/hitracemeter",
    ]

    sources = [ "//base/hiviewdfx/hitrace/interfaces/js/kits/napi/hitracemeter/napi_hitrace_meter.cpp" ]

    deps = [
      "//base/hiviewdfx/hilog/interfaces/native/innerkits:libhilog_${platform}",
      "//plugins/interfaces/native:ace_plugin_util_${platform}",
      "//plugins/libs/napi:napi_${target_os}",
    ]

    if (platform == "android") {
      deps += [
        "//foundation/arkui/ace_engine/adapter/android/build:libarkui_android",
      ]
    } else if (platform == "ios") {
      deps += [ "//foundation/arkui/ace_engine/adapter/ios/build:libarkui_ios" ]
    }

    subsystem_name = "plugins"
    part_name = "hitrace_meter"
  }
}

foreach(item, ace_platforms) {
  plugin_hitrace_meter_static("hitrace_meter_static_" + item.name) {
    platform = item.name
    defines = []
    cflags_cc = []
    config = {
    }

    if (defined(item.config)) {
      config = item.config
    }

    if (defined(config.defines)) {
      defines = config.defines
    }

    if (defined(config.cflags_cc)) {
      cflags_cc = config.cflags_cc
    }
  }
}
