# Copyright (c) 2022-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")

common_plugin_libs = [
  "ability_access_ctrl",
  "bridge",
  "common_event",
  "display",
  "convertxml",
  "fileio",
  "hilog",
  "hitrace_meter",
  "i18n",
  "multimedia_image",
  "ndk_raw_file",
  "net/http",
  "request",
  "running_lock",
  "test_plugin",
  "resource_manager",
  "uri",
  "url",
]

template("plugin_lib") {
  forward_variables_from(invoker, "*")

  plugin_name = target_name
  if (platform == "ios") {
    ohos_source_set(string_replace(target_name, "_", "")) {
      deps = [ "//plugins/$plugin_name:${plugin_name}_static_${platform}" ]

      part_name = plugin_name
      subsystem_name = "plugins"
    }
  } else {
    target_name_tmp = string_replace(target_name, "_", "")
    target_name_ext = string_replace(target_name_tmp, "/", "_")
    plugin_name_ext = string_replace(plugin_name, "/", "_")
    ohos_shared_library(target_name_ext) {
      deps = [ "//plugins/$plugin_name:${plugin_name_ext}_static_${platform}" ]

      relative_install_dir = "module"
      part_names = string_split(plugin_name, "/")
      part_name = part_names[0]
      subsystem_name = "plugins"
    }
  }
}
