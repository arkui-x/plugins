# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")

template("plugin_resource_manager_static") {
  forward_variables_from(invoker, "*")

  ohos_source_set(target_name) {
    defines += invoker.defines
    cflags_cc += invoker.cflags_cc

    include_dirs = [
      "//foundation/arkui",
      "$ace_root",
      "$ace_root/frameworks",
      "$root_out_dir/arkui/framework",
      "//plugins/interfaces/native",
      "//base/global/resource_management/frameworks/resmgr/include",
    ]

    sources = [
      "src/resource_manager_addon.cpp",
      "src/resource_manager_napi.cpp",
    ]

    public_deps = [
      "//plugins/interfaces/native:plugin_log",
      "//plugins/libs/napi:napi_${target_os}",
      "//plugins/libs/resmgr:resmgr_${target_os}",
    ]

    if (target_os == "android") {
      defines += [ "ANDROID_PLATFORM" ]
    }

    if (target_os == "ios") {
      defines += [ "IOS_PLATFORM" ]
    }

    if (defined(config.libace_target)) {
      deps = [ config.libace_target ]
    }

    defines += [
      "SUPPORT_GRAPHICS",
      "__ARKUI_CROSS__",
    ]

    subsystem_name = "plugins"
    part_name = "resource_manager"
  }
}

foreach(item, ace_platforms) {
  plugin_resource_manager_static("resource_manager_static_" + item.name) {
    defines = []
    cflags_cc = []
    config = {
    }

    if (defined(item.config)) {
      config = item.config
    }

    if (defined(config.defines)) {
      defines = config.defines
    }

    if (defined(config.cflags_cc)) {
      cflags_cc = config.cflags_cc
    }
  }
}
