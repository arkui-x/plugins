# Copyright (c) 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")

HILOG_NAPI_ROOT_DIR = "//base/hiviewdfx/hilog/interfaces/js/kits/napi/"

template("plugin_hilog_static") {
  forward_variables_from(invoker, "*")

  ohos_source_set(target_name) {
    defines += invoker.defines
    cflags_cc += invoker.cflags_cc
    include_dirs = [
      "//base/hiviewdfx/hilog/frameworks/libhilog/include",
      "//base/hiviewdfx/hilog/frameworks/libhilog/param/include",
      "//base/hiviewdfx/hilog/interfaces/native/innerkits/include",
      "//third_party/bounds_checking_function/include",
    ]
    if (is_arkui_x) {
      include_dirs += [
        "//base/hiviewdfx/hilog/interfaces/js/kits/napi/src/hilog/include/context",
        "//base/hiviewdfx/hilog/interfaces/js/kits/napi/src/common/napi",
      ]
    }
    sources = [
      "${HILOG_NAPI_ROOT_DIR}/src/common/napi/n_class.cpp",
      "${HILOG_NAPI_ROOT_DIR}/src/common/napi/n_func_arg.cpp",
      "${HILOG_NAPI_ROOT_DIR}/src/common/napi/n_val.cpp",
      "${HILOG_NAPI_ROOT_DIR}/src/hilog/module.cpp",
      "${HILOG_NAPI_ROOT_DIR}/src/hilog/src/hilog_napi.cpp",
      "${HILOG_NAPI_ROOT_DIR}/src/hilog/src/hilog_napi_base.cpp",
    ]

    deps = [
      "//base/hiviewdfx/hilog/interfaces/native/innerkits:libhilog_${platform}",
      "//plugins/interfaces/native:ace_plugin_util_${platform}",
      "//plugins/libs/napi:napi_${target_os}",
    ]

    subsystem_name = "plugins"
    part_name = "hilog"
  }
}

foreach(item, ace_platforms) {
  plugin_hilog_static("hilog_static_" + item.name) {
    platform = item.name

    defines = []
    cflags_cc = []
    config = {
    }

    if (defined(item.config)) {
      config = item.config
    }

    if (defined(config.defines)) {
      defines = config.defines
    }

    if (defined(config.cflags_cc)) {
      cflags_cc = config.cflags_cc
    }
  }
}
