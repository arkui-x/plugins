# Copyright (c) 2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/es2abc_config.gni")
import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")
import("//foundation/arkui/ace_engine/build/ace_gen_obj.gni")

subheader_path =
    "$ace_root/advanced_ui_component/subheader/interfaces/subheader.js"

es2abc_gen_abc("gen_subheader_abc") {
  src_js = rebase_path("$subheader_path")
  dst_file = rebase_path(target_out_dir + "/subheader.abc")
  in_puts = [ "$subheader_path" ]
  out_puts = [ target_out_dir + "/subheader.abc" ]
  extra_args = [ "--module" ]
}

if (target_os == "ios") {
  gen_obj("subheader_abc") {
    input = get_label_info(":gen_subheader_abc", "target_out_dir") +
            "/subheader.abc"
    output = target_out_dir + "/subheader_abc.c"
    snapshot_dep = [ ":gen_subheader_abc" ]
  }
} else if (target_os == "android") {
  gen_js_obj("subheader_abc") {
    input = get_label_info(":gen_subheader_abc", "target_out_dir") +
            "/subheader.abc"
    output = target_out_dir + "/subheader_abc.o"
    dep = ":gen_subheader_abc"
  }
}

template("plugin_arkui_advanced_subheader_static") {
  forward_variables_from(invoker, "*")
  ohos_source_set(target_name) {
    defines += invoker.defines
    cflags_cc += invoker.cflags_cc

    sources = [ "//plugins/arkui/advanced/subheader/subheader.cpp" ]

    deps = [ "//plugins/libs/napi:napi_${target_os}" ]

    if (target_os == "ios") {
      deps += [ ":gen_obj_src_subheader_abc" ]
    } else if (target_os == "android") {
      deps += [ ":subheader_abc" ]
    }

    subsystem_name = "plugins"
    part_name = "arkui_advanced_subheader"
  }
}

foreach(item, ace_platforms) {
  plugin_arkui_advanced_subheader_static(
      "arkui_advanced_subheader_static_" + item.name) {
    defines = []
    cflags_cc = []
    config = {
    }

    if (defined(item.config)) {
      config = item.config
    }

    if (defined(config.defines)) {
      defines = config.defines
    }

    if (defined(config.cflags_cc)) {
      cflags_cc = config.cflags_cc
    }
  }
}
