# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/es2abc_config.gni")
import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")
import("//foundation/arkui/ace_engine/build/ace_gen_obj.gni")

composetitlebar_path = "$ace_root/advanced_ui_component/composetitlebar/interfaces/composetitlebar.js"

es2abc_gen_abc("gen_composetitlebar_abc") {
  src_js = rebase_path("$composetitlebar_path")
  dst_file = rebase_path(target_out_dir + "/composetitlebar.abc")
  in_puts = [ "$composetitlebar_path" ]
  out_puts = [ target_out_dir + "/composetitlebar.abc" ]
  extra_args = [ "--module" ]
}

if (target_os == "ios") {
  gen_obj("composetitlebar_abc") {
    input = get_label_info(":gen_composetitlebar_abc", "target_out_dir") +
            "/composetitlebar.abc"
    output = target_out_dir + "/composetitlebar_abc.c"
    snapshot_dep = [ ":gen_composetitlebar_abc" ]
  }
} else if (target_os == "android") {
  gen_js_obj("composetitlebar_abc") {
    input = get_label_info(":gen_composetitlebar_abc", "target_out_dir") +
            "/composetitlebar.abc"
    output = target_out_dir + "/composetitlebar_abc.o"
    dep = ":gen_composetitlebar_abc"
  }
}

template("plugin_arkui_advanced_composetitlebar_static") {
  forward_variables_from(invoker, "*")
  ohos_source_set(target_name) {
    defines += invoker.defines
    cflags_cc += invoker.cflags_cc

    sources = [ "//plugins/arkui/advanced/composetitlebar/composetitlebar.cpp" ]

    deps = [ "//plugins/libs/napi:napi_${target_os}" ]

    if (target_os == "ios") {
      deps += [ ":gen_obj_src_composetitlebar_abc" ]
    } else if (target_os == "android") {
      deps += [ ":composetitlebar_abc" ]
    }

    subsystem_name = "plugins"
    part_name = "arkui_advanced_composetitlebar"
  }
}

foreach(item, ace_platforms) {
  plugin_arkui_advanced_composetitlebar_static(
      "arkui_advanced_composetitlebar_static_" + item.name) {
    defines = []
    cflags_cc = []
    config = {
    }

    if (defined(item.config)) {
      config = item.config
    }

    if (defined(config.defines)) {
      defines = config.defines
    }

    if (defined(config.cflags_cc)) {
      cflags_cc = config.cflags_cc
    }
  }
}
