# Copyright (c) 2026 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")

template("plugin_data_distributedkvstore_static") {
  forward_variables_from(invoker, "*")

  ohos_source_set(target_name) {
    defines += invoker.defines
    cflags_cc += invoker.cflags_cc

    deps = [ "//commonlibrary/c_utils/base:utilsbase" ]

    if (target_os == "android") {
      deps += [ "//foundation/distributeddatamgr/kv_store/interfaces/jskits/distributedkvstore/build/linux:data_distributedkvstore" ]
    } else if (target_os == "ios") {
      deps += [ "//foundation/distributeddatamgr/kv_store/interfaces/jskits/distributedkvstore/build/mac:data_distributedkvstore" ]
    }

    subsystem_name = "plugins"
    part_name = "data_distributedkvstore"
  }
}

foreach(item, ace_platforms) {
  plugin_data_distributedkvstore_static(
      "data_distributedkvstore_static_" + item.name) {
    defines = []
    cflags_cc = []
    config = {
    }

    if (defined(item.config)) {
      config = item.config
    }

    if (defined(config.defines)) {
      defines = config.defines
    }

    if (defined(config.cflags_cc)) {
      cflags_cc = config.cflags_cc
    }
  }
}
