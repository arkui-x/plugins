# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//build/config/components/ets_frontend/es2abc_config.gni")
import("//build/ohos.gni")
import("//commonlibrary/ets_utils/ets_utils_config.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")

ohos_source_set("stream_static") {
  branch_protector_ret = "pac_ret"

  sources = [ "native_module_stream.cpp" ]

  deps = [
    "$plugins_root/libs/napi:napi_${target_os}",
    "$util_module/stream:gen_obj_src_stream_abc",
    "$util_module/stream:gen_obj_src_stream_js",
  ]
}

template("plugin_util_stream_static") {
  forward_variables_from(invoker, "*")

  ohos_source_set(target_name) {
    defines += invoker.defines
    cflags_cc += invoker.cflags_cc

    deps = [ ":stream_static" ]

    subsystem_name = "plugins"
    part_name = "ets_utils"
  }
}

foreach(item, ace_platforms) {
  plugin_util_stream_static("util_stream_static_" + item.name) {
    defines = []
    cflags_cc = []
    config = {
    }

    if (defined(item.config)) {
      config = item.config
    }

    if (defined(config.defines)) {
      defines = config.defines
    }

    if (defined(config.cflags_cc)) {
      cflags_cc = config.cflags_cc
    }
  }
}
