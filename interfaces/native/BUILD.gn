# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")

ohos_source_set("plugin_log") {
  defines = [ "PLUGIN_LOG_TAG=\"Plugins\"" ]
  sources = [ "log.cpp" ]
  if (target_os == "android") {
    defines += [ "ANDROID_PLATFORM" ]
    aosp_deps = [ "shared_library:liblog" ]
  } else if (target_os == "ios") {
    include_dirs = [ "//third_party/bounds_checking_function/include" ]
    defines += [ "IOS_PLATFORM" ]
  }
}

config("plugin_util_config") {
  include_dirs = [ "//plugins/interfaces/native" ]
}

template("ace_plugin_util_static") {
  forward_variables_from(invoker, "*")

  ohos_source_set(target_name) {
    defines += invoker.defines
    cflags_cc += invoker.cflags_cc

    include_dirs = [
      "$ace_root",
      "$ace_root/frameworks",
    ]

    public_configs = [ ":plugin_util_config" ]

    sources = [
      "plugin_c_utils.cpp",
      "plugin_utils.cpp",
    ]

    deps = [
      ":plugin_log",
      "//foundation/arkui/napi:ace_napi",
    ]

    if (defined(config.libace_target)) {
      deps += [ config.libace_target ]
    }

    subsystem_name = "plugins"
    part_name = "plugin_utils"
  }
}

foreach(item, ace_platforms) {
  ace_plugin_util_static("ace_plugin_util_static_" + item.name) {
    defines = []
    cflags_cc = []
    config = {
    }

    if (defined(item.config)) {
      config = item.config
    }

    if (defined(config.defines)) {
      defines = config.defines
    }

    if (defined(config.cflags_cc)) {
      cflags_cc = config.cflags_cc
    }
  }

  if (item.name == "android") {
    ohos_shared_library("ace_plugin_util_" + item.name) {
      platform = item.name
      public_configs = [ ":plugin_util_config" ]
      deps = [ ":ace_plugin_util_static_${platform}" ]
      public_deps = [ "//foundation/arkui/napi:ace_napi" ]

      subsystem_name = "plugins"
      part_name = "plugin_utils"
    }
  } else {
    ohos_source_set("ace_plugin_util_" + item.name) {
      platform = item.name
      public_configs = [ ":plugin_util_config" ]
      deps = [ ":ace_plugin_util_static_${platform}" ]
      public_deps = [ "//foundation/arkui/napi:ace_napi" ]

      subsystem_name = "plugins"
      part_name = "plugin_utils"
    }
  }
}
