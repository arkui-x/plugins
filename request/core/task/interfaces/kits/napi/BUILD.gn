# Copyright (c) 2022-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")
import("//plugins/request/request.gni")

config("task_interfaces_config") {
  visibility = [ ":*" ]
  include_dirs = [ "include" ]
  cflags_cc = [ "-fexceptions" ]
}

config("task_interfaces_public_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "$third_party_path/node/src",
    "//third_party/bounds_checking_function/include",
    "$rq_task_path/frameworks/native/include",
    "$rq_task_path/frameworks/lib/include",
    "$rq_task_path/utils/include",
    "$rq_utils_path",
    "$plugin_interfaces_path",
  ]
  cflags_cc = [ "-fexceptions" ]
}

ohos_source_set("task_napi") {
  sources = [
    "src/async_call.cpp",
    "src/complete_notify.cpp",
    "src/fail_notify.cpp",
    "src/header_notify.cpp",
    "src/js_initialize.cpp",
    "src/js_notify.cpp",
    "src/js_ref_holder.cpp",
    "src/js_task.cpp",
    "src/napi_task.cpp",
    "src/napi_utils.cpp",
    "src/pause_notify.cpp",
    "src/progress_notify.cpp",
    "src/remove_notify.cpp",
    "src/response_notify.cpp",
    "src/resume_notify.cpp",
  ]

  configs = [ ":task_interfaces_config" ]
  public_configs = [ ":task_interfaces_public_config" ]

  deps = [
    "$plugin_interfaces_path/native:plugin_log",
    "$rq_task_path/frameworks/native:task_native",
    "//plugins/libs/napi:napi_${target_os}",
  ]

  if (target_os == "android") {
    defines = [ "ANDROID_PLATFORM" ]
  } else if (target_os == "ios") {
    defines = [ "IOS_PLATFORM" ]
  }

  defines += [
    "REQUEST_LOG_DOMAIN = 0xD001C00",
    "REQUEST_LOG_TAG = \"RequestJs\"",
  ]

  subsystem_name = "plugins"
  part_name = "request"
}
