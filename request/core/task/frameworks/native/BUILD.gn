# Copyright (c) 2022-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")
import("//plugins/request/request.gni")

config("task_native_config") {
  visibility = [ ":*" ]
  include_dirs = [ "include" ]
  cflags_cc = [ "-fexceptions" ]
}

config("task_native_public_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "$ace_root/frameworks/base/utils",
    "$third_party_path/node/src",
    "//third_party/bounds_checking_function/include",
    "$rq_task_path/utils/include",
    "$rq_task_path/frameworks/lib/include",
    "$rq_utils_path",
    "$plugin_interfaces_path",
    "//third_party/json/include",
  ]
  if (target_os == "android") {
    include_dirs += [ "$rq_task_path/frameworks/android/include" ]
  } else if (target_os == "ios") {
    include_dirs += [ "$rq_task_path/frameworks/ios/include" ]
  }
  cflags_cc = [ "-fexceptions" ]
}

ohos_source_set("task_native") {
  sources = [
    "src/config_json.cpp",
    "src/file_spec_json.cpp",
    "src/filter_json.cpp",
    "src/form_item_json.cpp",
    "src/progress_json.cpp",
    "src/response_json.cpp",
    "src/request_utils.cpp",
    "src/task.cpp",
    "src/task_info_json.cpp",
    "src/task_manager.cpp",
    "src/task_state_json.cpp",
  ]

  configs = [ ":task_native_config" ]
  public_configs = [ ":task_native_public_config" ]

  deps = [
    "$plugin_interfaces_path/native:plugin_log",
    "$rq_task_path/frameworks/lib:task_lib",
    "//plugins/libs/napi:napi_${target_os}",
  ]

  if (target_os == "android") {
    defines = [ "ANDROID_PLATFORM" ]
    deps += [ "$rq_task_path/frameworks/android:task_android" ]
  } else if (target_os == "ios") {
    defines = [ "IOS_PLATFORM" ]
    deps += [ "$rq_task_path/frameworks/ios:task_ios" ]
  }

  defines += [
    "REQUEST_LOG_DOMAIN = 0xD001C00",
    "REQUEST_LOG_TAG = \"RequestNative\"",
  ]

  subsystem_name = "plugins"
  part_name = "request"
}
