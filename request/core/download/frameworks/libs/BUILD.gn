# Copyright (C) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")
import("//plugins/request/request.gni")

config("download_lib_config") {
  visibility = [ ":*" ]
  include_dirs = [ "include" ]
  cflags_cc = [ "-fexceptions" ]
}

config("download_lib_public_config") {
  visibility = []
  include_dirs = [
    "$ace_root/frameworks/base/utils",
    "$third_party_path/curl/include",
    "$rq_utils_path",
    "$utils_base_path/include",
    "$rq_download_path/utils/include",
    "$plugin_interfaces_path",
  ]
}

ohos_source_set("download_lib") {
  sources = [
    "src/download_config.cpp",
    "src/download_info.cpp",
    "src/download_manager.cpp",
    "src/download_task.cpp",
  ]

  if (support_download_libcurl) {
    defines = [ "SUPPORT_DOWNLOAD_CURL" ]
    include_dirs = [ "$rq_download_path/frameworks/libcurl/include" ]
    sources +=
        [ "$rq_download_path/frameworks/libcurl/src/download_task_impl.cpp" ]
  } else {
    include_dirs = [ "$rq_download_path/frameworks/libplatform/include" ]
    sources += [ "$rq_download_path/frameworks/libplatform/src/download_task_plugin_impl.cpp" ]
  }

  configs = [ ":download_lib_config" ]

  public_configs = [ ":download_lib_public_config" ]

  deps = [
    "$plugin_interfaces_path/native:plugin_log",
    "$third_party_path/curl:curl",
  ]

  if (target_os == "android") {
    defines += [ "ANDROID_PLATFORM" ]
  }

  subsystem_name = "plugins"
  part_name = "request"
}
