# Copyright (c) 2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//plugins/bluetooth/bluetooth.gni")

ohos_source_set("bluetooth_baseprofile_static") {
  include_dirs = [
    "$bluetooth_napi_root/src/base_profile",
    "//plugins/bluetooth/common/include",
    "//plugins/bluetooth/common/mock",
  ]

  defines = [
    "BLUETOOTH_API_SINCE_10",
    "BT_LOG_DOMAIN = 0xD000100",
    "BT_LOG_TAG = \"bluetooth_napi\"",
    "BT_MODULE_NAME = \"bluetooth.ble\"",
    "ENABLE_NAPI_BLUETOOTH_MANAGER",
  ]

  sources = [
    "$bluetooth_napi_root/src/base_profile/napi_bluetooth_base_profile.cpp",
    "//plugins/bluetooth/baseprofile/native_module_base_profile.cpp",
  ]

  deps = [
    "$hilog_root/interfaces/native/innerkits:libhilog_${target_os}",
    "//plugins/bluetooth/common:bt_common_${target_os}",
    "//plugins/libs/napi:napi_${target_os}",
  ]

  if (is_android) {
    defines += [ "ANDROID_PLATFORM" ]
  } else if (is_ios) {
    defines += [ "IOS_PLATFORM" ]
  }

  subsystem_name = "plugins"
  part_name = "bluetooth_baseprofile"
}

template("plugin_bluetooth_baseprofile_static") {
  forward_variables_from(invoker, "*")

  ohos_source_set(target_name) {
    defines += invoker.defines
    cflags_cc += invoker.cflags_cc

    deps = [ ":bluetooth_baseprofile_static" ]

    subsystem_name = "plugins"
    part_name = "bluetooth_baseprofile"
  }
}

foreach(item, ace_platforms) {
  plugin_bluetooth_baseprofile_static(
      "bluetooth_baseprofile_static_" + item.name) {
    defines = []
    cflags_cc = []
    config = {
    }

    if (defined(item.config)) {
      config = item.config
    }

    if (defined(config.defines)) {
      defines = config.defines
    }

    if (defined(config.cflags_cc)) {
      cflags_cc = config.cflags_cc
    }
  }
}
