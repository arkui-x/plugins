# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")

template("plugin_common_event_plugin_static") {
  forward_variables_from(invoker, "*")

  ohos_source_set(target_name) {
    defines += invoker.defines
    cflags_cc += invoker.cflags_cc

    sources = [
      "common_event.cpp",
      "common_event_data.cpp",
      "common_event_js.cpp",
      "common_event_manager.cpp",
      "common_event_subscribe_info.cpp",
      "common_event_subscriber.cpp",
      "napi_common_event.cpp",
    ]

    deps = [
      "//foundation/arkui/napi:ace_napi",
      "//plugins/interfaces/native:ace_plugin_util_${platform}",
    ]

    if (platform == "android") {
      deps += [ "android/java:common_event_anroid_jni" ]
    } else if (platform == "ios") {
      deps += [ "ios:common_event_ios" ]
    }

    subsystem_name = "plugins"
    part_name = "common_event"
  }
}

foreach(item, ace_platforms) {
  plugin_common_event_plugin_static("common_event_static_" + item.name) {
    platform = item.name

    defines = []
    cflags_cc = []
    config = {
    }

    if (defined(item.config)) {
      config = item.config
    }

    if (defined(config.defines)) {
      defines = config.defines
    }

    if (defined(config.cflags_cc)) {
      cflags_cc = config.cflags_cc
    }
  }
}
